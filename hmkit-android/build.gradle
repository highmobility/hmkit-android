apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"

// deploy scripts need to be ordered and before `dependencies {` block. This way correct dependencies
// are resolved
apply from: "deploy.gradle"
apply from: "https://raw.githubusercontent.com/tonisives/tools/master/gradle/deploy-java-mavenCentral.gradle"
apply from: "https://raw.githubusercontent.com/tonisives/tools/master/gradle/test-shell-logging.gradle"

version ver."${deploy.id}"

android {
    compileSdkVersion ver.compileSdk
    namespace 'com.highmobility.hmkit'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion ver.compileSdk
        multiDexEnabled true

        ndk {
            moduleName = "hmbtcore"
            ldLibs "log"
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            buildConfigField("String", "VERSION_NAME", "\"${version}\"")
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            buildConfigField("String", "VERSION_NAME", "\"${version}\"")
        }
    }


    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    splits {
        abi {
            enable true
            reset()
            include 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
            universalApk true
        }
    }

    sourceSets.main {
        // This prevents the auto generation of Android.mk
        jniLibs.srcDir 'src/main/libs'
        // This is not necessary unless you have precompiled libraries in your project.
    }
    lint {
        abortOnError false
    }
}

dependencies {
    if (project.depLocation == 0) {
        api project(':hmkit-core-jni')
        implementation project(':hmkit-crypto')
        implementation project(':hmkit-utils')
    } else {
        api deps.crypto
        api deps.utils
        api deps.corejni
    }

    api deps.timber
    implementation deps.volley
    implementation deps.findBugs
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // its not easy to use ndk with junit(should build separate binary)
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.9.2'
    testImplementation 'androidx.test:core:1.5.0'

    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test:runner:1.5.1'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.5.0'
}